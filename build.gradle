buildscript {
    repositories {
        mavenCentral()
        maven {
            url = "https://plugins.gradle.org/m2/"
        }
    }
}

plugins {
    id "org.javamodularity.moduleplugin" version "1.8.15"
    id 'org.openjfx.javafxplugin' version "0.1.0"
}

tasks.register('copyPlugin', Copy) {

    group = 'build'
    description = 'Copies the plugins into the main project plugins folder'
    
    dependsOn ':org.speleodb.ariane.plugin.speleodb:build'

    doFirst {
        // Delete all files in the destination directory before copying
        file("./com.arianesline.plugincontainer/plugins").listFiles()?.each { file ->
            delete file
        }
        println 'Cleaned destination directory'
    }

    doLast {
        println 'Plugins Copied'
    }

    from "./org.speleodb.ariane.plugin.speleodb/build/libs"
    into "./com.arianesline.plugincontainer/plugins"

}

task copyAndRun(type: GradleBuild) {
    group = 'application'
    description = 'Copies the plugins and runs the plugin container application'
    
    tasks = ['copyPlugin', ':com.arianesline.plugincontainer:run']
}
subprojects {
    apply plugin: 'java-library'
    apply plugin: 'org.openjfx.javafxplugin'

    javafx {
        version = "23.0.1"
        modules = [ 'javafx.controls', 'javafx.base' ,'javafx.graphics','javafx.fxml','javafx.swing','javafx.media','javafx.web']
    }

    group = 'arianesline'
    version = '25.2.1'

    java {
        sourceCompatibility = 21
        targetCompatibility = 21
    }

    repositories {
        mavenCentral()
    }
    compileJava {
        options.compilerArgs += ['-Xlint:all', '-Xlint:-processing']
        options.deprecation = true
        options.warnings = true
    }
    
    test {
        jvmArgs = [
            '--enable-native-access=ALL-UNNAMED',
            '--add-opens=java.base/java.lang=ALL-UNNAMED'
        ]
        systemProperty 'java.awt.headless', 'true'
        systemProperty 'prism.order', 'sw'
        systemProperty 'prism.text', 't2k'
        systemProperty 'prism.lcdtext', 'false'
        systemProperty 'prism.subpixeltext', 'false'
    }
    
}
